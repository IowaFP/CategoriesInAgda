{-# OPTIONS --without-K #-}

module Categories.Constructions.FAlgebra where 

open import Categories.Prelude
open import Categories.Category 
open import Categories.Functor 
open import Categories.NaturalTransformation 
open import Categories.Constructions.Initial
open import Categories.Reasoning.Hom 
open import Categories.Category.Subcategory

--------------------------------------------------------------------------------
-- F-algebras
--
-- An F-Algebra in category ğ’, for endofunctor F, is a 2-tuple (A , Ï†) where
-- A âˆˆ ğ’ (the *carrier*) and Ï† : F A â‡’ A. F-Algebras form a category Alg 
-- whose objects are F-Algebras and a morphism from (A , Ï†) to (B , Ïˆ) is 
-- an arrow f : A â‡’ b such that the following diagram commutes.
--          Ï† 
--     F A ----> A 
--      |        |
-- F(f) |        | f
--      v        v
--     F B ----> B 
--           Ïˆ 
--
-- An initial F-Algebra (Î¼F , in) is given by the least fixed-point of F. That is,
-- we have in : F Î¼F â‰ƒ Î¼F : out. By initiality, we have a unique morphism 
-- â¦… Ï† â¦† : Î¼F â†’ C for any algebra (C , Ï†).
--           in 
--     F Î¼F ----> Î¼F
--      |        |
-- F(Ï†) |        | â¦… Ï† â¦† 
--      v        v
--     F C ----> C 
--           Ï†  

module _ (ğ’ : Category o a e)
         (F : Endofunctor ğ’) where 
  open Category ğ’ 
  open Functor F 

  record FAlg : Set (o âŠ” a) where 
    constructor _,_
    field
      Carrier : Obj
      alg : Fâ‚€ Carrier â‡’ Carrier

  open FAlg public 

  -- A fixed-point is an isomorphic F-Algebra
  record FixedPoint : Set (o âŠ” a âŠ” e) where 
    constructor _,_ 
    field 
      alg : FAlg 
      iso : isIso ğ’ (alg .FAlg.alg)

module _ {ğ’ : Category o a e}
         {F : Endofunctor ğ’} where 

  open Category ğ’ 
  open Functor F 
  open HomReasoning ğ’ 

  open FAlg 
  record Hom (Ï† Ïˆ : FAlg ğ’ F) : Set (a âŠ” e) where 
    constructor _,_ 
    field 
      hom : Ï† .Carrier â‡’ Ïˆ .Carrier 
      commutes : hom âˆ˜ Ï† .alg â‰ˆ Ïˆ .alg âˆ˜ fmap hom

  _âˆ˜FA_ : âˆ€ {Ï† Ïˆ Î¶} â†’ Hom Ïˆ Î¶ â†’ Hom Ï† Ïˆ â†’ Hom Ï† Î¶ 
  _âˆ˜FA_ {Ï† = (A , Ï†)} {Ïˆ = (B , Ïˆ)} {Î¶ = (C , Î¶)} (f , comm-f) (g , comm-g) = 
   f âˆ˜ g , 
   (begin 
      f âˆ˜ g âˆ˜ Ï†              â‰ˆâŸ¨ (assáµ£ â¨¾ cong-âˆ˜áµ£ comm-g) âŸ© 
      f âˆ˜ (Ïˆ âˆ˜ fmap g)       â‰ˆâŸ¨ (assâ‚— â¨¾ cong-âˆ˜â‚— comm-f) âŸ© 
      (Î¶ âˆ˜ fmap f) âˆ˜ fmap g  â‰ˆâŸ¨ (assáµ£ â¨¾ cong-âˆ˜áµ£ (sym-â‰ˆ (F-âˆ˜ g f))) âŸ© 
      Î¶ âˆ˜ fmap (f âˆ˜ g) âˆ)
  
  IdHom : âˆ€ {Ï† : FAlg ğ’ F} â†’ Hom Ï† Ï† 
  IdHom {Ï† = (A , Ï†)} = Id , (begin 
    Id âˆ˜ Ï†  â‰ˆâŸ¨ left-id âŸ©
    Ï†       â‰ˆâŸ¨ (sym-â‰ˆ right-id â¨¾ cong-âˆ˜áµ£ (sym-â‰ˆ F-id)) âŸ© 
    Ï† âˆ˜ fmap Id âˆ)

-- ------------------------------------------------------------------------------
-- F-Algebras form a category

module _ (ğ’ : Category o a e)
         (F : Endofunctor ğ’) where 
  open Category ğ’
  open Functor F 
  open IsEquivalence
  open Hom
  open HomReasoning ğ’ 

  FAlgebras : Category (o âŠ” a) (a âŠ” e) e 
  FAlgebras .Category.Obj = FAlg ğ’ F 
  FAlgebras .Category._â‡’_ =  Hom
  FAlgebras .Category._âˆ˜_ = _âˆ˜FA_
  FAlgebras .Category.Id = IdHom
  FAlgebras .Category._â‰ˆ_ (f , _) (g , _) =  f â‰ˆ g
  FAlgebras .Category.eqv  .refl = refl-â‰ˆ
  FAlgebras .Category.eqv  .sym = sym-â‰ˆ
  FAlgebras .Category.eqv  .trans = trans-â‰ˆ
  FAlgebras .Category.cong-âˆ˜  = cong-âˆ˜
  FAlgebras .Category.right-id =  right-id
  FAlgebras .Category.left-id = left-id
  FAlgebras .Category.assâ‚— = assâ‚—

  -- ------------------------------------------------------------------------------
  -- The fixed-points of F form a full subcategory of the category FAlgebras
  open FixedPoint 

  FixedPoints : Category (o âŠ” a âŠ” e) (a âŠ” e) e 
  FixedPoints = FullSubcategory FAlgebras (FixedPoint ğ’ F) alg 

------------------------------------------------------------------------------
-- Initial objects in the category of F-algebras yield catamorphisms

  module _ (Ï† : FAlg ğ’ F) 
           (ini : isInitial FAlgebras Ï†) where 
    open isInitial ini
    open FAlg Ï† renaming (Carrier to Î¼F ; alg to In)

    -- The catamorphism
    â¦…_â¦† : (Ïˆ : FAlg ğ’ F) â†’ Hom Ï† Ïˆ 
    â¦… Ïˆ â¦† = ! Ïˆ

    -- ------------------------------------------------------------------------------
    -- Lambek's lemma: If F has an initial F-Algebra Ï†, then Ï† is a fixed-point of F.

    Lambek : FixedPoint ğ’ F  
    Lambek = 
      Ï† , Out , 
      (begin 
        In âˆ˜ Out â‰ˆâŸ¨ !-id  Inâˆ˜Out âŸ© 
        Id âˆ) , 
      (begin 
        Out âˆ˜ In â‰ˆâŸ¨ Out-commutes âŸ© 
        fmap In âˆ˜ fmap Out â‰ˆâŸ¨ sym-â‰ˆ (F-âˆ˜ Out In) âŸ© 
        fmap (In âˆ˜ Out) â‰ˆâŸ¨ (F-cong (!-id Inâˆ˜Out) â¨¾ F-id) âŸ© 
        Id âˆ)
      where 
        open Hom â¦… (Fâ‚€ Î¼F , fmap In) â¦† renaming (hom to Out ; commutes to Out-commutes)
        Inâˆ˜Out : Hom Ï† Ï† 
        Inâˆ˜Out = In âˆ˜ Out , (begin 
          In âˆ˜ Out âˆ˜ In             â‰ˆâŸ¨ (assáµ£ â¨¾ cong-âˆ˜áµ£ Out-commutes) âŸ© 
          In âˆ˜ (fmap In âˆ˜ fmap Out) â‰ˆâŸ¨ cong-âˆ˜áµ£ (sym-â‰ˆ (F-âˆ˜ Out In)) âŸ© 
          In âˆ˜ fmap (In âˆ˜ Out) âˆ)

    -- ------------------------------------------------------------------------------
    -- Smyth and Plotkin's Lemma 1: an initial F-Algebra is also
    -- an initial fixed-point in the category of F-fixed points.

    SmythPlotkin : isInitial FixedPoints Lambek 
    SmythPlotkin .isInitial.! (Ïˆ , _) = â¦… Ïˆ â¦†
    SmythPlotkin .isInitial.unique = unique
